variable "test1_bucket_name" {
  description = "The name of the S3 bucket"
  type        = string
}

variable "test1_iam_user_name" {
  description = "The name of the IAM user for read-only access"
  type        = string
}

variable "test1_region" {
  description = "AWS region"
  type        = string
  default     = "ap-northeast-1"
}

provider "aws" {
  region = var.test1_region
}

# S3バケットの作成
resource "aws_s3_bucket" "test1_bucket" {
  bucket = var.test1_bucket_name 

  tags = {
    Name        = var.test1_bucket_name
    Environment = "Development"
  }
}

# バケットACLの設定
resource "aws_s3_bucket_ownership_controls" "test1_bucket_ownership" {
  bucket = aws_s3_bucket.test1_bucket.id
  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

# パブリックアクセスブロック設定の無効化
resource "aws_s3_bucket_public_access_block" "test1_public_access_block" {
  bucket = aws_s3_bucket.test1_bucket.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

resource "aws_s3_bucket_acl" "test1_bucket_acl" {
  depends_on = [aws_s3_bucket_ownership_controls.test1_bucket_ownership]
  bucket = aws_s3_bucket.test1_bucket.id
  acl    = "public-read"
}

# バケットポリシーの設定
resource "aws_s3_bucket_policy" "test1_read_only_policy" {
  depends_on = [aws_s3_bucket_public_access_block.test1_public_access_block]
  bucket = aws_s3_bucket.test1_bucket.id
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Sid       = "PublicReadGetObject"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:GetObject"
        Resource  = "${aws_s3_bucket.test1_bucket.arn}/*"
      }
    ]
  })
}


# IAMユーザーの作成
resource "aws_iam_user" "test1_read_only_user" {
  name = var.test1_iam_user_name
}

# IAMポリシーの作成 (S3 Read-Only)
resource "aws_iam_policy" "test1_s3_read_only_policy" {
  name        = "${var.test1_iam_user_name}_S3ReadOnly"
  description = "Read-only access to the S3 bucket"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect   = "Allow",
        Action   = ["s3:GetObject"],
        Resource = "${aws_s3_bucket.test1_bucket.arn}/*"
      }
    ]
  })
}

# IAMポリシーをユーザーにアタッチ
resource "aws_iam_user_policy_attachment" "attach_test1_read_only_policy" {
  user       = aws_iam_user.test1_read_only_user.name
  policy_arn = aws_iam_policy.test1_s3_read_only_policy.arn
}

# IAMアクセスキーの作成
resource "aws_iam_access_key" "test1_access_key" {
  user = aws_iam_user.test1_read_only_user.name
}

output "test1_access_key_id" {
  value = aws_iam_access_key.test1_access_key.id
  sensitive = true
}

output "test1_bucket_name" {
  value = aws_s3_bucket.test1_bucket.bucket
}



